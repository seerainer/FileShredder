plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.5'
}

group = 'io.github.seerainer.fileshredder'
version = '0.1.4'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java.srcDirs = [ 'src' ]
        resources.srcDir 'resources'
    }
}

ext {
    swtArtifact = 'org.eclipse.swt.'
    os = System.getProperty("os.name")
    arch = System.getProperty("os.arch")
}

switch (os) {
    case 'Linux':
        swtArtifact += 'gtk.linux.'
        break
    case 'Mac OS X':
        swtArtifact += 'cocoa.macosx.'
        break
    default:
        if (os.startsWith("Win")) {
            swtArtifact += 'win32.win32.'
        }
}

switch (arch) {
    case ['amd64', 'x86_64']:
        swtArtifact += 'x86_64'
        break
    case 'aarch64':
        swtArtifact += 'aarch64'
        break
}

dependencies {
    implementation("org.eclipse.platform:${swtArtifact}:3.128.0") {
        exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'    
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.GRAAL_VM
    }
}

application {
    mainClass = 'io.github.seerainer.fileshredder.FileShredder'
}

run {
    if (os.equals("Mac OS X")) {
        jvmArgs += '-XstartOnFirstThread'
    }
}

graalvmNative {
    testSupport = false
    toolchainDetection = true
    
    binaries {
        main {
            imageName = 'FileShredder'
            mainClass = application.mainClass
            buildArgs.add('-Ob')
            buildArgs.add('-march=compatibility')
            buildArgs.add('--gc=epsilon')
            buildArgs.add('--no-fallback')
            buildArgs.add('-R:MinHeapSize=256m')
            buildArgs.add('-R:MaxHeapSize=2g')
            buildArgs.add('-R:MaxNewSize=512m')
            useFatJar = true
        }
    }
}

tasks.withType(Jar).configureEach {
    exclude("META-INF/*.SF")
    exclude("META-INF/*.RSA")
    manifest {
	    attributes 'Main-Class': application.mainClass
    }
}
